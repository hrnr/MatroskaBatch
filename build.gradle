subprojects {
	apply plugin: 'java'

	group = 'cz.hrnr'
	version = '1.0-SNAPSHOT'

	description = """MatroskaBatch"""

	sourceCompatibility = 1.8
	targetCompatibility = 1.8

	repositories {
		mavenCentral()
	}
}

project(':common') {
	dependencies {
		compile group: 'org.apache.commons', name: 'commons-lang3', version:'3.4'
	}
}

project(':desktop') {
	apply plugin: 'application'
	apply from: 'javafx.plugin'

	dependencies {
		compile project(':common')
	}

	mainClassName = 'cz.hrnr.matroskabatch.gui.MatroskaBatch'

	javafx {
		mainClass = mainClassName
	}
}

project(':server') {
	apply plugin: 'war'

	dependencies {
		compile project(':common')
		compile group: 'org.glassfish.jersey.containers', name: 'jersey-container-servlet', version: '2.19'
		compile group: 'org.glassfish.jersey.media', name: 'jersey-media-moxy', version: '2.19'
	}

	def deployHome = '/var/lib/tomcat8/webapps'

	war {
		baseName = 'MatroskaBatchServer'
		webXml = file('src/webapp/WEB-INF/web.xml')
	}

	task deploy(type: Copy) {
		from war
		into "${deployHome}"
		include '*.war'
	}
}


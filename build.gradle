buildscript {
    repositories {
        maven { url "http://nexus.qmino.com/content/repositories/miredot" }
        mavenCentral()
    }
    dependencies {
        classpath 'com.qmino:miredot-plugin:1.6.2'
    }
}

subprojects {
	apply plugin: 'java'

	group = 'cz.hrnr'
	version = '1.0'

	description = """MatroskaBatch"""

	sourceCompatibility = 1.8
	targetCompatibility = 1.8

	repositories {
		mavenCentral()
	}
}

project(':common') {
	dependencies {
		compile group: 'org.apache.commons', name: 'commons-lang3', version:'3.4'
		compile group: 'org.glassfish.jersey.core', name: 'jersey-client', version: '2.21'
		runtime group: 'org.glassfish.jersey.media', name: 'jersey-media-jaxb', version: '2.21'
		compile group: 'commons-logging', name: 'commons-logging', version: '1.2'
	}
}

project(':desktop') {
	apply plugin: 'application'
	//apply from: 'javafx.plugin'

	dependencies {
		compile project(':common')
	}

	mainClassName = 'cz.hrnr.matroskabatch.gui.MatroskaBatch'

	//javafx {
	//	mainClass = mainClassName
	//}
}

project(':server') {
	apply plugin: 'war'
	apply plugin: 'miredot'

	miredot {
	  source = sourceSets.main.allJava
	  destination = sourceSets.main.output
	  license = 'UHJvamVjdHxjei5ocm5yLnNlcnZlcnwyMDE2LTA5LTEyfHRydWUjT' +
	  			'UMwQ0ZDNENJUlpqMTIrYVJwVW9CUHRRR2lrY3JxaEVBaFVBajZXaW' +
	  			'dFd3JqTFR1Wmk1aysxc0cza3FvMCtBPQ=='
	}

	dependencies {
		compile project(':common')
		compile group: 'javax.servlet', name: 'javax.servlet-api', version: '3.1.0'
		compile group: 'org.glassfish.jersey.containers', name: 'jersey-container-servlet', version: '2.21'
		runtime group: 'org.glassfish.jersey.media', name: 'jersey-media-moxy', version: '2.21'
	}

	def deployHome = '/var/lib/tomcat8/webapps'

	war {
		baseName = 'MatroskaBatchServer'
		webXml = file('src/webapp/WEB-INF/web.xml')
	}

	task deploy(type: Copy) {
		from war
		into "${deployHome}"
		include '*.war'
	}
}

